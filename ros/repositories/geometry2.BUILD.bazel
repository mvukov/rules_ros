""" Builds geometry2.
"""

load(
    "@rules_ros//ros:cc_defs.bzl",
    "cc_ros_binary",
    "cc_ros_library",
)
load(
    "@rules_ros//ros:interfaces.bzl",
    "cc_ros_interface_library",
    "py_ros_interface_library",
    "ros_interface_library",
)
load("@rules_python//python:defs.bzl", "py_library")

ros_interface_library(
    name = "tf2_msgs",
    srcs = glob([
        "tf2_msgs/action/*.action",
        "tf2_msgs/msg/*.msg",
        "tf2_msgs/srv/*.srv",
    ]),
    visibility = ["//visibility:public"],
    deps = [
        "@ros_common_msgs//:actionlib_msgs",
        "@ros_common_msgs//:geometry_msgs",
    ],
)

cc_ros_interface_library(
    name = "cc_tf2_msgs",
    deps = [":tf2_msgs"],
)

py_ros_interface_library(
    name = "py_tf2_msgs",
    deps = [":tf2_msgs"],
)

cc_ros_library(
    name = "tf2",
    srcs = glob(["tf2/src/*.cpp"]),
    hdrs = glob(["tf2/include/**/*.h"]),
    includes = ["tf2/include"],
    visibility = ["//visibility:public"],
    deps = [
        ":cc_tf2_msgs",
        "@boost.signals2",
        "@boost.thread",
        "@boost.unordered",
        "@boost.foreach",
        "@console_bridge",
        "@ros_common_msgs//:cc_geometry_msgs",
        "@roscpp_core//:rostime",
    ],
)

cc_ros_library(
    name = "tf2_ros",
    srcs = [
        "tf2_ros/src/buffer.cpp",
        "tf2_ros/src/buffer_client.cpp",
        "tf2_ros/src/buffer_server.cpp",
        "tf2_ros/src/static_transform_broadcaster.cpp",
        "tf2_ros/src/transform_broadcaster.cpp",
        "tf2_ros/src/transform_listener.cpp",
    ],
    hdrs = glob(["tf2_ros/include/tf2_ros/*.h"]),
    copts = ["-w"],
    includes = ["tf2_ros/include"],
    visibility = ["//visibility:public"],
    deps = [
        ":cc_tf2_msgs",
        ":tf2",
        "@ros_actionlib//:actionlib",
        "@ros_comm//:message_filters",
        "@ros_comm//:roscpp_lib",
        "@ros_common_msgs//:cc_actionlib_msgs",
        "@ros_common_msgs//:cc_geometry_msgs",
    ],
)

cc_ros_binary(
    name = "tf2_ros_buffer_server",
    srcs = ["tf2_ros/src/buffer_server_main.cpp"],
    visibility = ["//visibility:public"],
    deps = [":tf2_ros"],
)

cc_ros_binary(
    name = "tf2_ros_static_transform_publisher",
    srcs = ["tf2_ros/src/static_transform_broadcaster_program.cpp"],
    visibility = ["//visibility:public"],
    deps = [":tf2_ros"],
)

cc_library(
    name = "cc_tf2_py",
    linkstatic = 0,
    srcs = ["tf2_py/src/tf2_py.cpp"],
    hdrs = ["tf2_py/src/python_compat.h"],
    visibility = ["//visibility:public"],
    deps = [
        ":tf2",
        "@rules_python//python/cc:current_py_cc_headers",
    ]
)

py_library(
    name = "tf2_py",
    srcs = glob(["tf2_py/src/**/*.py"]),
    imports = ["tf2_py/src"],
    visibility = ["//visibility:public"],
    data = [":cc_tf2_py"],
    deps = ["@rules_python//python/runfiles", ":tf2"],
)

py_library(
    name = "tf2_ros_py",
    srcs = glob(["tf2_ros_py/*.py"]),
    imports = ["."],
    visibility = ["//visibility:public"],
    deps = [
        ":tf2_py",
        ":py_tf2_msgs",
        "@ros_comm//:rospy",
        "@ros_std_msgs//:py_std_msgs",
        "@ros_common_msgs//:py_sensor_msgs",
        "@ros_common_msgs//:py_geometry_msgs",
    ],
)
